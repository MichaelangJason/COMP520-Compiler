# #  comment
# () grouping
# [] optional
# *  zero or more
# +  one or more
# |  alternative

program    ::= (include)* (structdecl | vardecl | fundecl | funproto)* EOF

include    ::= "#include" STRING_LITERAL

structdecl ::= structtype "{" (vardecl)+ "}" ";"    # structure declaration

vardecl    ::= type IDENT ("[" INT_LITERAL "]")* ";" # variable declaration, (e.g. int a;), or multi-dimensional array declaration, (e.g. int a[2][5];)

fundecl    ::= type IDENT "(" params ")" block       # function declaration

funproto   ::= type IDENT "(" params ")" ";"         # function prototype

type       ::= ("int" | "char" | "void" | structtype) ("*")*
structtype ::= "struct" IDENT

params     ::= [ type IDENT ("[" INT_LITERAL "]")* ("," type IDENT ("[" INT_LITERAL "]")*)* ]

stmt       ::= block
             | "while" "(" exp ")" stmt              # while loop
             | "if" "(" exp ")" stmt ["else" stmt]   # if then else
             | "return" [exp] ";"                    # return
             | exp ";"                               # expression statement, e.g. a function call
             | "continue" ";"                        # continue
             | "break" ";"                           # break

block      ::= "{" (vardecl)* (stmt)* "}"

exp        ::= "(" exp ")" #beta1
             | exp "=" exp              #alpha1             # assignment
             | (IDENT | INT_LITERAL) #beta2
             | ("-" | "+") exp #beta3
             | CHAR_LITERAL #beta4
             | STRING_LITERAL #beta5
             | exp (">" | "<" | ">=" | "<=" | "!=" | "==" | "+" | "-" | "/" | "*" | "%" | "||" | "&&") exp  #alpha2 # binary operators
             | arrayaccess | fieldaccess | valueat | addressof | funcall | sizeof | typecast

funcall      ::= IDENT "(" [ exp ("," exp)* ] ")" # function call #beta6
arrayaccess  ::= exp "[" exp "]"                  # array access #alpha3
fieldaccess  ::= exp "." IDENT                    # structure field member access #alpha4
valueat      ::= "*" exp                          # Value at operator (pointer indirection) #beta7
addressof    ::= "&" exp                          # Address-of operator #beta8
sizeof       ::= "sizeof" "(" type ")"            # size of type #beta9
typecast     ::= "(" type ")" exp                 # type casting #beta10


